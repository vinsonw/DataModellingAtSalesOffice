> 整理自`Power BI商业数据分析项目实战`一书的第六章
>
> 作者自称使用了黄成明的`数据化管理`一书的`周销售指数`概念, 但是实际实现(以及本书的实现)和`数据化管理`一书中的定义有关键不同

### 1. 将数据源聚合为合适的数据粒度

```dax
销售汇总 = 
SUMMARIZE (
	'销售明细', 
	'销售明细'[销售日期], 
	"销售额", SUM ( '销售明细'[销售额] )
)
```

添加一些必要的时间维度列:

```dax
月份 = MONTH ( '销售汇总'[销售日期] )
星期 = WEEKDAY ( '销售汇总'[销售日期], 2 )
月份&星期 = '销售汇总'[月份] & '销售汇总'[星期]
```

### 2. 构建销售系数

#### 2.1 计算每个月, 每周七天的平均销售额

新建`X月星期X平均销售`列:

```dax
X月星期X平均销售 = 
VAR sum_of_this_weekday_in_this_month = 
SUMX (
	FILTER ('销售汇总', '销售汇总'[月份&星期] = EARLIER( '销售汇总'[月份&星期] ) ),
	'销售汇总'[销售额]
)
VAR count_of_this_weekday_in_this_month = 
COUNTAX (
	FILTER ('销售汇总', '销售汇总'[月份&星期] = EARLIER( '销售汇总'[月份&星期] ) ),
	'销售汇总'[星期] //应该写任意一列均可, 只是为了对FILTER返回的表的行计数
)
RETURN 
	DIVIDE ( sum_of_this_weekday_in_this_month, count_of_this_weekday_in_this_month )
```

#### 2.2 计算每个月每周七天的系数

新建`X月星期X系数`列:

```dax
X月星期X系数 = 
DIVIDE (
	'销售汇总'[X月星期X平均销售],
	MINX (
		FILTER ('销售汇总', '销售汇总'[月份] = EARLIER ( '销售汇总'[月份] ) ),
		'销售汇总'[X月星期X平均销售]
	)
)
```

以当月平均销售额最小的星期X的平均销售额为系数的基准(该销售额对应的系数是1)

#### 2.3 提取出销售系数表

2.2得到的销售系数表是by天的, 其中有重复系数, 去重得到新表:

```dax
销售系数表 = 
SUMMARIZE (
	'销售汇总',
	'销售汇总'[月份],
	'销售汇总'[星期],
	'销售汇总'[月份&星期],
	'销售汇总'[X月星期X系数]
)
```

SUMMARIZE函数的原理和数据透视表极度相似: 第一个参数是要汇总的表, 中间的若干参数是"行字段", 最后两个参数是值字段(分别指定值列的列名和值列要进行的计算). 这里只是使用了"行字段"的去重功能, 没有值列计算.

### 3. 分解销售目标

利用上述得到的销售系数表, 将2018年的销售总目标分解到每一天.

#### 3.1 构建最终想要得到的`2018销售目标分解`表

首先, 构建日期列

```dax
2018销售目标分解 = 
CALENDAR ( DATE ( 2018, 1, 1), DATE (2018, 12, 31) )
```

然后在该表中补充必要的月份, 星期, 月份&星期列.

以`销售目标`表和`销售系数表`为LOOPUP表, 以`2018销售目标分解`为事实表, 建立表之间的关系.

在`2018销售目标分解`表中添加计算列:

```dax
X月星期X系数 = RELATED ( '销售系统表'[X月星期X系数] )
当月销售总目标 = RELATED ( '销售目标'[销售目标] )
```

#### 3.2 计算当月每天销售占比(重点)

继续在`2018销售目标分解`表中添加计算列:

```dax
当天在当月的销售占比 = 
DIVIDE (
	'2018销售目标分解'[X月星期X系数],
	SUMX (
		FILTER ( '2018销售目标分解', '2018销售目标分解'[月份] = EARLIER ( '2018销售目标分解'[月份] )),
		'2018销售目标分解'[X月星期X系数]
	)
)
```

#### 3.3 计算每天的销售目标

接着在`2018销售目标分解`表中添加计算列:

```dax
当天销售目标 = 
'2018销售目标分解'[当月销售总目标] * '2018销售目标分解'[当天在当月的销售占比]
```

`当天销售目标`就是我们期望的最终分解结果.

Test git.